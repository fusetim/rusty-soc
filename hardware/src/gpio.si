/// GPIO (General Purpose Input/Output) Peripheral
unit gpio_peripheral(
    input  uint1        rst,
    // Memory-mapped I/O interface
    input  uint1        mem_en,      // enable signal
    input  uint8        mem_addr,
    input  uint32       mem_wr_data,
    input  uint4        mem_wr_en,
    output! uint32      mem_rd_data(32b0),
    // GPIO signals
    output uint8        leds(8h00),
    input  uint7        btns,
    // SPI - SD Card
    output! uint1        sd_cs(1b1),
    output! uint1        sd_mosi(1b0),
    output! uint1        sd_clk(1b0),
    input  uint1         sd_miso,
    // SPI - OLED Display
    output! uint1        oled_cs(1b1),
    output! uint1        oled_mosi(1b0),
    output! uint1        oled_clk(1b0),
    output! uint1        oled_dc(1b0),
    output! uint1        oled_res(1b1),
    // Audio Viz
    output uint1        audio_viz_en(1b0)
) {
    // Register addresses (word-aligned)
    uint10 REG_LED_RW(10h00); // LED Read/Write
    uint10 REG_BTN_R(10h04); // Button Read-only
    uint10 REG_SPI_SD_RW(10h08); // SPI SD Card Read/Write
    uint10 REG_SPI_OLED_RW(10h0C); // SPI OLED Display Read/Write
    uint10 REG_AUDIO_VIZ_RW(10h10); // Audio Viz Read/Write (for enabling/disabling visualization)

    always {
        uint10 mem_addr_ext = {mem_addr, 2b00}; // Extend to word-aligned address
        uint1 leds_access            = (mem_addr_ext == REG_LED_RW); 
        uint1 button_access          = (mem_addr_ext == REG_BTN_R);
        uint1 spi_sd_access          = (mem_addr_ext == REG_SPI_SD_RW);
        uint1 spi_oled_access        = (mem_addr_ext == REG_SPI_OLED_RW);
        uint1 audio_viz_access       = (mem_addr_ext == REG_AUDIO_VIZ_RW);

        // Handle read from GPIO peripheral
        mem_rd_data = (mem_en & leds_access) ? {24b0, leds} :
                      (mem_en & button_access) ? {25b0, btns} :
                      (mem_en & spi_sd_access) ? {28b0, sd_miso, sd_clk, sd_mosi, sd_cs} :
                      (mem_en & spi_oled_access) ? {27b0, oled_res, oled_dc, oled_clk, oled_mosi, oled_cs} :
                      (mem_en & audio_viz_access) ? {31b0, audio_viz_en} :
                      32b0;

        // Handle write to GPIO peripheral
        if (~rst & mem_en & (mem_wr_en != 4b0000)) {
            // Update LEDs
            uint8 led_mask = leds_access ? mem_wr_data[8,8] : 8h00;
            leds           = (leds & ~led_mask) | (mem_wr_data[0,8] & led_mask);

            // BTN - read-only, no write operation

            // Update SPI SD Card signals
            uint3 spi_sd_mask = spi_sd_access ? mem_wr_data[8,3] : 3b000;
            sd_cs          = spi_sd_mask[0,1] ? mem_wr_data[0,1] : sd_cs;
            sd_mosi        = spi_sd_mask[1,1] ? mem_wr_data[1,1] : sd_mosi;
            sd_clk         = spi_sd_mask[2,1] ? mem_wr_data[2,1] : sd_clk;

            // Update SPI OLED Display signals
            uint5 spi_oled_mask = spi_oled_access ? mem_wr_data[8,5] : 5b00000;
            oled_cs        = spi_oled_mask[0,1] ? mem_wr_data[0,1] : oled_cs;
            oled_mosi      = spi_oled_mask[1,1] ? mem_wr_data[1,1] : oled_mosi;
            oled_clk       = spi_oled_mask[2,1] ? mem_wr_data[2,1] : oled_clk;
            oled_dc        = spi_oled_mask[3,1] ? mem_wr_data[3,1] : oled_dc;
            oled_res       = spi_oled_mask[4,1] ? mem_wr_data[4,1] : oled_res;

            // Update Audio Viz enable signal
            audio_viz_en   = (audio_viz_access && mem_wr_en[0,1]) ? mem_wr_data[0,1] : audio_viz_en;

        }
        if (rst) {
            leds = 8h00; // Reset LEDs to 0 on reset
        }

        // SIMULATION ONLY: debug output for LEDs
        $$if SIMULATION then
            if (mem_en & leds_access) {
                __display("LEDs: %b (%b(%b) > %h)",leds,mem_addr,mem_wr_en,mem_wr_data);
            }
            if (mem_en & button_access) {
                __display("Buttons read: %b",btns);
            }
            if (mem_en & spi_sd_access) {
                __display("SPI SD Card: CS=%b, MOSI=%b, MISO=%b, CLK=%b",
                    sd_cs, sd_mosi, sd_miso, sd_clk);
            }
            if (mem_en & spi_oled_access) {
                __display("SPI OLED Display: CS=%b, MOSI=%b, CLK=%b, DC=%b, RES=%b",
                    oled_cs, oled_mosi, oled_clk, oled_dc, oled_res);
            }
            if (mem_en & audio_viz_access) {
                __display("Audio Viz Enable: %b", audio_viz_en);
            }
        $$end
    }
}
