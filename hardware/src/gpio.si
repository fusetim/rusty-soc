/// GPIO (General Purpose Input/Output) Peripheral
unit gpio_peripheral(
    input  uint1        rst,
    // Memory-mapped I/O interface
    input  uint1        mem_en,      // enable signal
    input  uint8        mem_addr,
    input  uint32       mem_wr_data,
    input  uint4        mem_wr_en,
    output! uint32      mem_rd_data(32b0),
    // GPIO signals
    output uint8        leds(8h00),
    input  uint7        btns,
    // SPI - SD Card
    output! uint1        sd_cs(1b1),
    output! uint1        sd_mosi(1b0),
    output! uint1        sd_clk(1b0),
    input  uint1         sd_miso,
) {
    // Register addresses (word-aligned)
    uint10 REG_LED_RW(10h00); // LED Read/Write
    uint10 REG_BTN_R(10h04); // Button Read-only
    uint10 REG_SPI_SD_RW(10h08); // SPI SD Card Read/Write

    always {
        uint10 mem_addr_ext = {mem_addr, 2b00}; // Extend to word-aligned address
        uint1 leds_access            = (mem_addr_ext == REG_LED_RW); 
        uint1 button_access          = (mem_addr_ext == REG_BTN_R);
        uint1 spi_sd_access          = (mem_addr_ext == REG_SPI_SD_RW);

        // Handle read from GPIO peripheral
        mem_rd_data = (mem_en & leds_access) ? {24b0, leds} :
                      (mem_en & button_access) ? {25b0, btns} :
                      (mem_en & spi_sd_access) ? {28b0, sd_miso, sd_clk, sd_mosi, sd_cs} :
                      32b0;

        // Handle write to GPIO peripheral
        if (~rst & mem_en & (mem_wr_en != 4b0000)) {
            // Update LEDs
            uint8 led_mask = leds_access ? mem_wr_data[8,8] : 8h00;
            leds           = (leds & ~led_mask) | (mem_wr_data[0,8] & led_mask);

            // Update SPI SD Card signals
            uint3 spi_sd_mask = spi_sd_access ? mem_wr_data[8,3] : 3b000;
            sd_cs          = spi_sd_mask[0,1] ? mem_wr_data[0,1] : sd_cs;
            sd_mosi        = spi_sd_mask[1,1] ? mem_wr_data[1,1] : sd_mosi;
            sd_clk         = spi_sd_mask[2,1] ? mem_wr_data[2,1] : sd_clk;

            // BTN - read-only, no write operation
        }
        if (rst) {
            leds = 8h00; // Reset LEDs to 0 on reset
        }

        // SIMULATION ONLY: debug output for LEDs
        $$if SIMULATION then
            if (mem_en & leds_access) {
                __display("LEDs: %b (%b(%b) > %h)",leds,mem_addr,mem_wr_en,mem_wr_data);
            }
            if (mem_en & button_access) {
                __display("Buttons read: %b",btns);
            }
            if (mem_en & spi_sd_access) {
                __display("SPI SD Card: CS=%b, MOSI=%b, MISO=%b, CLK=%b",
                    sd_cs, sd_mosi, sd_miso, sd_clk);
            }
        $$end
    }
}
